
plugins {
    id 'java'
    id 'application'
    // GUI
    id 'org.openjfx.javafxplugin' version '0.0.8'
    // Formatter
    id "com.diffplug.gradle.spotless" version "3.27.1"

    // IDE support
    id 'idea'
    id 'eclipse'
}

repositories {
    jcenter()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls','javafx.fxml']
}

spotless {
    java {
        googleJavaFormat('1.3')
        custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }
        importOrder 'java', 'javax', 'org', 'com'
        indentWithSpaces(4)
        trimTrailingWhitespace()
        removeUnusedImports()
    }
    format 'misc', {
        target '**/.gitignore', '**/*.gradle', '**/*.md', '**/*.sh'
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
    implementation 'com.google.guava:guava:28.1-jre'
    // Logging using slf4j API and logback backend
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    // Use JUnit Jupiter API and Engine for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

application {
    // Define the main class for the application.
    mainClassName = 'com.watson.propert.tycoon.App'
}

test {
    useJUnitPlatform()
}
